// *******************************************************************************************************************
// *** Render all clips from a project as .JPG or .PNG sequences (without duplicate frames) along with an .XML file
// *******************************************************************************************************************

//Preparing
message = "Rendering single clip with XML file..."
tv_lockdisplay message
//
tv_GetprojectName
projectpath = result
tv_WriteTextFile "Exists" '"'projectpath'"'
testPath = result
// *** Check if file is saved
IF CMP(testPath,projectpath)==0 || CMP(projectpath,"/")==1 //testpath is voor windows, slash-check is voor mac? Misschien is testpath wel overbodig en kan t uberhaupt met alleen de slash-check.
	tv_warn "Please save project and try again!"
	EXIT
END
//
tv_Request "Image type?|JPG|PNG" //1|0
IF CMP(result, 1)
	imageType = jpg
ELSE
	imageType = png
END
// *** Get project info
tv_ProjectInfo
PARSE result projectName projectWidth projectHeight projectPixelAspect projectFramerate projectField projectProjectStartframe
tv_firstImage
projectIn = result
tv_LastImage
projectOut = result
projectpath = Replace(projectpath,'\','/') //Making the script work on Windows and Unix, Windows has directory seperated by \ and mac and linux with /
strlen = LEN(projectpath)
last = LastPos(projectpath,"/") // Getting the directory where the TVPP file is saved
projectDir = LeftString(projectpath,last-1) // Getting the directory where the TVPP file is saved
projectName = RightString(projectpath,strlen-last) //Getting the name of the tvpp file without the path
strlen = LEN(projectName) //Getting the name of the tvpp file without ".tvpp"
//
last = LastPos(projectName,".")
IF CMP(last,"0")==1
	projectbase = projectName
ELSE
	projectbase = LeftString(projectName,last-1)
END
last_v = FindString(projectbase, "_v", 1) //File should be name _v001_L or something
IF CMP(last_v,"0")==1
	projectName = projectbase
ELSE
	projectName = LeftString(projectbase,last_v-1)
END
// *** Create folder
FOR i = 1 TO 1000
	RenderFolder = projectName"_clips"i
	saveDir = projectDir"/"RenderFolder
	tv_WriteTextFile "Exists" '"'saveDir'"'
	IF CMP (result,"")==1 //Folder doesn't exist yet!
		projectName = RenderFolder
		i = 1001
	END
END
saveDir = projectDir"/"projectName
tv_Request "Render all clips as ."imageType" sequences in subfolders of '"saveDir"'?|OK|Cancel" //1|0
IF result == 0
	EXIT
END
tv_WriteTextFile "MkDir" '"'saveDir'"'
TotalXMLfile = saveDir"/"projectName".xml"
XMLbegin(TotalXMLfile, projectName, projectIn, projectOut, projectFramerate)
// *** Duplicate project so we don't have to undo any layer deleting/merging
tv_ProjectDuplicate

// **********************************
// *** ALLE CLIPS DOORLOPEN START ***
// **********************************
loop=1
i=0
addSomeFrames = 0
clipOffset = -1 // Start
colorLabel = "Blue"
WHILE loop
	tv_clipenumid -1 i
 	clipID = result
	IF CMP(clipID,"none")==1
		loop=0
	ELSE
		tv_clipHidden clipID
		IF CMP(result,"0")==1
			tv_clipSelect clipID
		END
	END
	i=i+1
	IF CMP(clipID,"none")==0
		IF CMP(colorLabel, "Blue")
			colorLabel = "Rose"
		ELSE
			colorLabel = "Blue"
		END
		tv_clipname clipID //*?
		clipName = result
		clipName = i"_"clipName // 001_Untitled for example
		// *** Delete all invisible layers, because they won't get merged correctly by TVPaint.
		DeleteInvisibleLayers() //Function is in Include file
		// *** Fix multiply layers if working on both standard and Pro TVPaint versions
		FixMultiply()
		PARAM none

		IF CMP(imageType, "png")
			tv_saveMode "PNG"
			ext = ".png"
			tv_AlphaSaveMode "PreMultiply" // Of moet dit NoPreMultiply zijn?
			tv_Background "NONE"
		ELSE
			tv_saveMode "JPG"
			ext = ".jpg"
			tv_Background "COLOR"
		END
		//
		// Save Current Frame So I can set the timelinecursor back at the end of the script
		tv_layerGetImage
		startImage = result
		//WAARSCHUWING: shot 001 moet "/001" heten. Ik denk dat de clip gewoon "Walk_R" mag heten			
		renderDir = projectDir"/"RenderFolder
		clipDir = projectDir"/"RenderFolder"/"clipName
		tv_WriteTextFile "MkDir" '"'renderDir'"'
		tv_WriteTextFile "MkDir" '"'clipDir'"'
		// Here I get the in point and out point of the clip if the marks is disabled I get the clip in out instead
		//get clip in
		tv_markIn clip
		PARSE result frame state
		IF CMP(state,"set ")==1
			inpoint = frame
		ELSE
			tv_firstImage
			inpoint = result
		END
		//Get Clip Out
		tv_markOut clip
		PARSE result frame state
		IF CMP(state,"set ")==1
			outpoint = frame
		ELSE
			tv_LastImage
			outpoint = result
		END
		//Because I have to use tv_projectsavesequence to use the camera out I have to get the clip in and out in the project timeline
		//For this I use tv_projectcurrentframe
		tv_layerImage inpoint
		tv_projectcurrentframe
		projIN = result
		tv_projectcurrentframe projIN
		tv_layerImage outpoint
		tv_projectcurrentframe
		projOUT = result
		tv_projectcurrentframe projOUT
		//Getting the name of the current clip to file prefix
		tv_startFrame
		startFrame = inpoint+result
		//Setting the name of the first file
		frame = addZeros("_",startFrame,5)
		renderFile = clipDir"/"clipName""frame""ext
		//Saving the sequence
		//Create a layer at the in and out point in case we need to render an empty alpha frame to reach in/out of the clip
		tv_LayerCreate tempLayer
		tempLayerID = result
		tv_LayerAnim
		tv_LayerShift tempLayerID inpoint
		tv_LayerPostBehavior layerID "hold"
		tv_LayerCreate tempLayer
		tempLayerID = result
		tv_LayerAnim
		tv_LayerShift tempLayerID outpoint
		tv_LayerPreBehavior tempLayerID "hold"
		tv_layerMergeAll
		ExportImagesAndXML(clipDir, clipName, inpoint, outpoint, clipOffset)
		//Adding clip offset so next clip can calculate starting frame
		clipOffset = clipOffset + curImage
	END
	tv_Background "COLOR" //Turn color background back on.
END
// *** End of clip loop ***
// *** End overall XML
XMLend(TotalXMLfile)
//Finishing
tv_projectClose
tv_warn "ALL CLIPS RENDERED."

// *****************************
// *** Include extra functions
// *****************************
#INCLUDE "Include.grg"