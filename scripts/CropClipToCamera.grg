//Preparing
tv_UndoOpenStack
tv_UpdateUndo
tv_LockDisplay "Cropping clip to camera..."

//Remembering current layer and frame
tv_LayerCurrentID
BeginLayer = result
tv_LayerGetImage
beginImage = result

//Getting project size
tv_getWidth
parse result projectWidth
tv_getHeight
parse result ProjectHeight
centX = projectWidth/2
centY = ProjectHeight/2
tv_FrameRate 666 info
PARSE result projectFramerate previewFramerate
//Getting cam info
tv_CameraEnumPoints
parse result camX camY camAngle camSize

//Loop trough all layers/instances
LayerRun = 1
i = 0
WHILE LayerRun
	tv_LayerGetID i
	curLID= result	
	IF CMP(curLID, "NONE") == 0
		//*** START RUN ON LAYER
        tv_LayerSet curLID
        tv_LayerInfo
		PARSE result LayerDisplay LayerPosition LayerOpacity LayerName LayerType LayerStart LayerEnd LayerPreLightTable LayerPostLightTable LayerSelection
       	CurImage = LayerStart
       	InstanceRun = 1
		WHILE InstanceRun
			tv_LayerImage CurImage
			//*** START RUN ON INSTANCE
			//Show message
			Message = CONCAT("Cropping layer to camera | Layer ",(LayerName))
			Message = CONCAT(Message, (" | Image : "))
			Message = CONCAT(Message, (CurImage))
			tv_LockDisplay Message
			//Getting image
			tv_brushcut 0 0 projectWidth ProjectHeight 1 0
			//Adjusting image
			newSize = ProjectHeight / (ProjectHeight*camSize) * 100
			newAngle = -camAngle
			tv_restorebrush "size" newSize
			tv_restorebrush "handle" camX camY
			tv_restorebrush "angle" newAngle
			//Pasting image
			tv_dot centX centY
			//*** END RUN ON INSTANCE
			tv_exposureNext //Goes to next exposure
			CurImage = result
			tv_exposureinfo CurImage
			PARSE result type rest
			IF CMP(type, "Head") == 0
				InstanceRun = 0
			END
		END
		//Recompute exposures because a lot might be ofscreen and we need less frames now.
		tv_LayerRecomputeExposure
  		//*** END RUN ON LAYER
		i = i + 1
	ELSE		
		LayerRun = 0
	END
END
//Resetting camera
tv_CameraInfo projectWidth ProjectHeight none projectFramerate 1
tv_CameraSetPoint 0 centX centY 0 1

//Resetting layer and frame to starting position
tv_LayerImage beginImage
tv_LayerSet BeginLayer

//Finishing
tv_undoclosestack
tv_unlockdisplay