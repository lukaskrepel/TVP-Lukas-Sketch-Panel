// Variables
dialogueLayerName = "DIALOGUE"
dialogueLayerColor = 7

// Ask user to insert the dialogue
tv_ReqString "multiline" "Dialogue|"
IF CMP(result, "Cancel") || CMP(result, "")
	EXIT
END
text_to_add = Replace("'"result"'", "\n", "' '")" 'none'"
//text_to_add = result
//text_to_add = "'"text_to_add"'"
//text_to_add = Replace(text_to_add, "\n", "' '")
//text_to_add = text_to_add" 'none'" // 'none' is always used as a terminator in the packed string
Rest = "" // remaining string
LineCount = 1	// Item count
Lines(0) = 0 // Array of lines
While CMP(Rest,"none") == 0
	Parse text_to_add Line Rest
	Lines(LineCount) = Line
	text_to_add = Rest
	LineCount = LineCount + 1
END

// ----------------------------------------------------
// Remember settings to revert to after script finished
// ----------------------------------------------------
// Remember starting color
tv_GetAPen
PARSE result R G B A
// Remember starting paper
tv_paperinfo
beginPaper = result
parse result z paperHardness z paperInvert z paperSize z paperAngle z paperOffsetX z paperOffsetY z paperFlipX z paperFlipY z paperActive z paperName
// Remember starting shape (line etc)
tv_getactiveshape
beginActiveShape = result
// Remember starting brush
tv_restorebrush backup
beginBrush = result
// Remember starting layer
tv_LayerCurrentID
beginLayer = result
tv_LayerGetImage // get original frame number
parse result OriginalFrame // original frame number
// TODO also remember brush stamp (origin/alpha/etc)

// Loop backwards trough layers to find DIALOGUE layer:
layerRun = 1
layerPos = 0
WHILE layerRun
	tv_LayerGetID layerPos
	lid = result
	IF CMP(lid,"NONE")==0
		// Start run on layer
		tv_LayerInfo lid
		PARSE result layerDisplay layerPosition layerOpacity layerName layerType layerStart layerEnd layerPrelighttable layerPostlighttable layerSelection
		IF CMP(layerName, dialogueLayerName)==1
			tv_layerSet lid
			layerRun = 0
		END
		// End run on layer
		layerPos = layerPos + 1
	ELSE
		// Create or go to the dialogue layer
		tv_LayerCreate dialogueLayerName
		DialogueLayerID = result
		tv_layercolor "set" 0 dialogueLayerColor
		tv_LayerMove 0 // Move the dialogue layer to top
		tv_layershift DialogueLayerID OriginalFrame
		layerRun = 0
	END
END

// Set text tool:
fontName = "Consolas"
fontSize = 50
resetVal = 1 // * 1 = true
letterVal = 0 // * 0 = Word
borderSize = 5
borderR = 255
borderG = 255
borderB = 255
stepVal = 9999999 // * Super big step so word is only drawn once
tv_textTool reset resetVal letter letterVal text text font fontName size fontSize border borderSize border_col borderR borderG borderB step stepVal
// Set text color:
textColR = 0
textColG = 150
textColB = 255
tv_SetAPen textColR textColG textColB

// Clear frame of dialogue layer
tv_layeranim
currentFrame = tv_projectcurrentframe
tv_exposurebreak currentFrame
tv_Clear 0

// Paste brush at center of screen
tv_ProjectInfo
PARSE result projectname projectwidth projectheight projectpixelaspect projectframerate projectfield projectprojectstartframe
x = projectwidth / 2
y = 120

FOR i = 1 to LineCount-1 // loop to display elements which are now in an array 
	Line = Lines(i)
	tv_TextTool text '"'Line'"'
	button = 0 // left mouse button
	tv_Dot x y button
	y = y + 60
END

// Revert to starting layer
//tv_layerSet beginLayer

// Revert to starting brush/tool
tv_brushrestore beginBrush // Reset brush
//TODO Reset brush stamp mode (origin/alpha/etc)
tv_SetActiveShape beginActiveShape // Reset shape mode
tv_SetAPen R" "G" "B" "A // Reset A pen color to start color
tv_paper "active" paperActive "offsetx" paperOffsetX "offsety" paperOffsetY "flipx" paperFlipX "flipy" paperFlipY "hardness" paperHardness "invert" paperInvert "size" paperSize "angle" paperAngle "name" paperName

// Include
#INCLUDE "Include.grg"