// Variables
ScriptName = "render_clips_with_xml"

// Load settings
tv_ReadUserString ScriptName "Extension" "none"
Extension = result
tv_ReadUserString ScriptName "Camera" "none"
Camera = result
IF CMP(IsPro(), 1) == 0
	Camera = 0
END
tv_ReadUserString ScriptName "Overwrite" "none"
Overwrite = result

ClipsToRender = "_"
ClipRun = 1
i = 0
ExportClipCount = 0
WHILE ClipRun
	tv_ClipEnumID -1 i
 	ClipID = result
	IF CMP(ClipID, "none") == 1
		ClipRun = 0
	ELSE
		tv_ClipInfo	ClipID
		PARSE result d Name d IsCurrent d IsHidden d IsSelected d StoryboardStartFrame d firstFrame d LastFrame d frameCount d MarkIn d MarkOut d ColorIdx
		IF CMP(IsSelected, 1) || CMP(IsCurrent, 1)
			ExportClipCount = ExportClipCount + 1
			ClipsToRender = ClipsToRender"1"
		ELSE
			ClipsToRender = ClipsToRender"0"
		END
	END
	i = i + 1
END


// New settings
tv_GetModifiers
PARSE result Shift Ctrl Alt
IF CMP(Shift, "1") == 1 || CMP(Compositing, "none") || CMP(Extension, "none") || CMP(Camera, "none") || CMP(Overwrite, "none")
	// Extenstion
	tv_Request "Image Type?|.JPG|.PNG" //1|0
	IF CMP(result, 1)
		Extension = "jpg"
	ELSE
		Extension = "png"
	END
	tv_WriteUserString ScriptName "Extension" Extension
	// Camera
	IF CMP(IsPro(), 1) == 1
		tv_Request "Export View?|Camera (Only if you know what you are doing)|Project (Default)" //1|0
		Camera = result
		tv_WriteUserString ScriptName "Camera" Camera
	END
	// Overwrite
	tv_Request "Use existing (highest numbered) clips folder?|Yes (Only if you know what you are doing)|No (Default)" //1|0
	Overwrite = result
	tv_WriteUserString ScriptName "Overwrite" Overwrite
	// Warn
	tv_Warn ("New settings saved.\n Click button again to render.")
	EXIT
END
tv_SaveMode Extension

//Preparing
message = "Rendering all clips with XML file..."
tv_LockDisplay message
tv_LockUser
//
tv_GetProjectName
ProjectPath = result
tv_WriteTextFile "Exists" '"'ProjectPath'"'
TestPath = result
// *** Check if file is saved
IF CMP(TestPath,ProjectPath) == 0 || CMP(ProjectPath, "/") == 1 //TestPath is voor windows, slash-check is voor mac? Misschien is TestPath wel overbodig en kan t uberhaupt met alleen de slash-check.
	tv_Warn "Please save project and try again!"
	EXIT
END

// Get project info
tv_ProjectInfo
PARSE result ProjectName projectWidth ProjectHeight projectPixelAspect projectFramerate projectField projectProjectStartFrame
tv_FirstImage
ProjectIn = result
tv_LastImage
ProjectOut = result
ProjectPath = Replace(ProjectPath,'\','/') //Making the script work on Windows and Unix, Windows has directory seperated by \ and mac and linux with /
StrLen = LEN(ProjectPath)
last = LastPos(ProjectPath, "/") // Getting the directory where the TVPP file is saved
ProjectDir = LeftString(ProjectPath,Last - 1) // Getting the directory where the TVPP file is saved
ProjectName = RightString(ProjectPath, StrLen - Last) // Getting the name of the tvpp file without the path
StrLen = LEN(ProjectName) // Getting the name of the tvpp file without ".tvpp"
//
last = LastPos(ProjectName, ".")
IF CMP(last, "0") == 1
	ProjectBase = ProjectName
ELSE
	ProjectBase = LeftString(ProjectName,Last - 1)
END
last_v = FindString(ProjectBase, "_v", 1) //  File should be name _v001_L or something
IF CMP(last_v, "0") == 1
	ProjectName = ProjectBase
ELSE
	ProjectName = LeftString(ProjectBase,last_v-1)
END
// *** Create folder
FOR i = 1 TO 1000
	RenderFolder = ProjectName"_clips"i
	SaveDir = ProjectDir"/"RenderFolder
	tv_WriteTextFile "Exists" '"'SaveDir'"'
	IF CMP (result, "") == 1 // Folder doesn't exist yet!
		IF CMP(Overwrite, 1)
			IF i > 1
				i = i - 1
				RenderFolder = ProjectName"_clips"i
				//tv_Warn ("WARNING: Re-using existing clips folder!")
			END
		END
		ProjectName = RenderFolder
		i = 1001
	END
END
SaveDir = ProjectDir"/"ProjectName
ClipString = "current clip"
ExtraS = ""
IF ExportClipCount > 1
	ExtraS = "s"
	ClipString = ExportClipCount" clips"
END
tv_Request "Render "ClipString" as ."Extension" sequence"ExtraS" in subfolder"ExtraS" of this location?\n\n"ProjectDir"/\n"RenderFolder"|OK|Cancel" //1|0
IF result == 0
	EXIT
END
message = "Rendering "ExportClipCount" clip"ExtraS" with XML file..."
tv_LockDisplay message
tv_LockUser
//
tv_SaveMode Extension // Needed for Standard version because it exports with tv_SaveSequence
tv_WriteTextFile "MkDir" '"'SaveDir'"'
TotalXMLfile = SaveDir"/"ProjectName".xml"
XMLbegin(TotalXMLfile, ProjectName, ProjectIn, ProjectOut, projectFramerate)

//MohoTemplate = "/Users/lukas/Nextcloud/meggamma/Club\ Baboo/SHOTS/Construction/CS21_Elasmosaurus/EMPTY\ TEXT\ MOHO\ DOC.mohoproj"
//NewMohoFile = SaveDir"/"ProjectName".moho"
//CreateMohoFile(MohoTemplate, NewMohoFile)

// *** Duplicate project so we don't have to undo any layer deleting/merging
tv_ProjectDuplicate
// **********************************
// *** ALLE CLIPS DOORLOPEN START ***
// **********************************
loop = 1
i = 0
AddSomeFrames = 0
clipOffset = 0//-1 // Start
colorLabel = "Blue"
//tv_Warn ("before loop")
WHILE loop
	tv_ClipEnumID -1 i
 	ClipID = result
	IF CMP(ClipID, "none") == 1
		loop = 0
	ELSE
		RenderClip = CHAR(ClipsToRender, (i + 2)) // String starts with an "_" because a 0 needs a prefix.
		IF CMP(RenderClip, "1") == 1
			// This clip should not be skipped!
			tv_ClipSelect clipID
		ELSE
			// This clip should be skipped!
			clipID = "none" // Ugly ugly code, this is a bit confusing, we want to skip this clip though, this works...
		END
	END
	i = i + 1
	IF CMP(clipID, "none") == 0
		IF CMP(colorLabel, "Blue")
			colorLabel = "Rose"
		ELSE
			colorLabel = "Blue"
		END
		tv_ClipName
		ClipName = result
		UnderscorePos = FindString(ClipName, "_", 1)
		IF UnderscorePos < 1
			clipNumber = addZeroesPostfix("_",i,3)
			ClipName = clipNumber""ClipName // 001_Untitled for example
		END
		// *** Delete all invisible layers, because they won't get merged correctly by TVPaint.
		DeleteInvisibleLayers() //Function is in Include file
		// *** Fix multiply layers if working on both standard and Pro TVPaint versions
		FixMultiply()
		PARAM none
		// * EINDE TEST
		IF CMP(Extension, "png")
			tv_SaveMode "PNG"
			ext = ".png"
			tv_AlphaSaveMode "PreMultiply" // Of moet dit NoPreMultiply zijn?
			tv_Background "NONE"
		ELSE
			tv_SaveMode "JPG"
			ext = ".jpg"
			tv_Background "COLOR" 255 255 255
		END
		renderDir = ProjectDir"/"RenderFolder
		tv_WriteTextFile "MkDir" '"'renderDir'"'
		clipDir = ProjectDir"/"RenderFolder"/"ClipName
		tv_WriteTextFile "MkDir" '"'clipDir'"'		
		//Clear in/out marks of clip (hack)
		tv_MarkIn clear
		tv_MarkOut clear
		//Here I get the in point and out point of the clip if the marks is disabled I get the clip in out instead
		//get clip in
		tv_MarkIn clip
		PARSE result frame state
		IF CMP(state, "set ") == 1
			InPoint = frame
		ELSE
			tv_FirstImage
			InPoint = result
		END
		//Get Clip Out
		tv_MarkOut clip
		PARSE result frame state
		IF CMP(state, "set ") == 1
			OutPoint = frame
		ELSE
			tv_LastImage
			OutPoint = result
		END
		//Because I have to use tv_Projectsavesequence to use the camera out I have to get the clip in and out in the project timeline
		//For this I use tv_ProjectCurrentFrame
		tv_LayerImage InPoint
		tv_ProjectCurrentFrame
		ProjIn = result
		tv_ProjectCurrentFrame ProjIn
		tv_LayerImage OutPoint
		tv_ProjectCurrentFrame
		ProjOut = result
		tv_ProjectCurrentFrame ProjOut
		//Getting the name of the current clip to file prefix
		tv_StartFrame
		StartFrame = InPoint + result
		//Setting the name of the first file
		frame = addZeros("",StartFrame,5)
		RenderFile = clipDir"/"ClipName""frame""ext
		//Saving the sequence
		//Create a layer at the in and out point in case we need to render an empty alpha frame to reach in/out of the clip
		tv_LayerCreate tempLayer
		tempLayerID = result
		tv_LayerAnim
		tv_LayerShift tempLayerID InPoint
		tv_LayerPostBehavior layerID "hold"
		tv_LayerCreate tempLayer
		tempLayerID = result
		tv_LayerAnim
		tv_LayerShift tempLayerID OutPoint
		tv_LayerPreBehavior tempLayerID "hold"
		tv_LayerMergeAll
		Whitespace = "    "
		FrameRange = OutPoint - InPoint + 1
		Message = "Rendering...\n"Whitespace""renderDir"/"Whitespace"\n"ClipName"\n("FrameRange" frames)"
		tv_LockDisplay Message
		ExportImagesAndXML(clipDir, ClipName, InPoint, OutPoint, clipOffset)
		// Adding clip offset so next clip can calculate starting frame
		clipOffset = clipOffset + CurImage
	END
	tv_Background "COLOR" 255 255 255 //Turn color background back on.
END
// *** End of clip loop ***
// *** End overall XML
XMLend(TotalXMLfile)
//Finishing
tv_ProjectClose
tv_UnlockDisplay
tv_UnlockUser

tv_Warn (ExportClipCount" CLIPS RENDERED.")

// *****************************
// *** Include extra functions
// *****************************
#INCLUDE "Include.grg"