Param None

tv_LayerCurrentID
StartLayerID = result
tv_LayerGetImage
parse result StartFrame
NextFrame = StartFrame + 1
Allow = true

layerRun = 1

tv_LastImage
LastImage = result
IF StartFrame == LastImage
	Allow = false
	layerRun = 0
END

// First we check if we want to be able to remove a clip exposure at all, or if that would destroy a certain drawing.
i = 0
WHILE layerRun
	tv_LayerGetID i
	LayerID = result
	IF CMP(LayerID, "none") == 0		
		// START RUN ON LAYER
		tv_layerSet LayerID
		tv_exposureinfo StartFrame
		exposureType = result
		IF CMP(exposureType, "none") == 0
			tv_exposureinfo NextFrame
			parse result exposureType FauxFixe d
			IF CMP(exposureType, "Head") == 1 || FauxFixe > 0
				Allow = false // Next frame is Head or FauxFixe.
				layerRun = 0
			END
			IF CMP(exposureType, "None") == 1
				tv_layerpostbehavior LayerID
				PostBehaviour = result
				IF CMP(PostBehaviour, "Hold") == 0
					Allow = false // Next frame is empty and layer stops.
					layerRun = 0
				END
			END
		ELSE
			// Check if this layer starts after the current frame AND if its pre-behaviour is not HOLD.
			tv_LayerInfo
			PARSE result LayerDisplay LayerPosition LayerOpacity LayerName LayerType LayerStart LayerEnd LayerPrelighttable LayerPostlighttable LayerSelection
			IF CMP(LayerStart, StartFrame+1)
				tv_layerprebehavior LayerId
				PostBehaviour = result
				IF CMP(PostBehaviour, "hold") == 0
					Allow = false
				END
			END
		END
		// END RUN ON LAYER
	ELSE
		layerRun = 0
	END
	i = i + 1
END

// If Allow == true, let's go and do the thing!
IF CMP(Allow, true) == 1
	layerRun = 1
	i = 0
	WHILE layerRun
		tv_LayerGetID i
		LayerID = result
		IF CMP(LayerID, "none") == 0		
			// START RUN ON LAYER
			tv_layerSet LayerID
			Frame = StartFrame
			tv_LayerImage Frame
			tv_exposureinfo Frame
			exposureType = result
			IF CMP(exposureType, "none") == 0
				WHILE CMP(exposureType, "head") == 0
					tv_LayerGetImage
					Frame = result - 1
					tv_LayerImage Frame
					tv_exposureinfo Frame
					exposureType = result
				END
				// On head of exposure
				tv_exposureinfo Frame
				exposureType = result
				tv_exposurenext
				nextInstance = result
				ExposureLength = nextInstance - Frame - 1
				tv_ExposureSet Frame ExposureLength
			ELSE
				tv_LayerInfo
				PARSE result LayerDisplay LayerPosition LayerOpacity LayerName LayerType LayerStart LayerEnd LayerPrelighttable LayerPostlighttable LayerSelection
				IF LayerStart > StartFrame
					tv_LayerShift LayerID LayerStart-1
				END
			END
			// END RUN ON LAYER
		ELSE
			layerRun = 0
		END
		i = i + 1
	END
//ELSE
	//print ("Sorry, can't Allow...")
END

tv_LayerImage StartFrame
tv_layerSet StartLayerID