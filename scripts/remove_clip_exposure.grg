Param None

tv_LayerCurrentID
StartLayerID = result
tv_LayerGetImage
parse result StartFrame
NextFrame = StartFrame + 1
Shrink = true

layerRun = 1

tv_LastImage
LastImage = result
IF StartFrame == LastImage
	Shrink = false
	layerRun = 0
END

i = 0
WHILE layerRun
	tv_LayerGetID i
	LayerID = result
	IF CMP(LayerID, "none") == 0		
		// START RUN ON LAYER
		tv_layerSet LayerID
		tv_exposureinfo StartFrame
		exposureType = result
		IF CMP(exposureType, "none") == 0
			tv_exposureinfo NextFrame
			parse result exposureType FauxFixe d
			IF CMP(exposureType, "Head") == 1 || FauxFixe > 0
				Shrink = false // Next frame is Head or FauxFixe.
				layerRun = 0
			END
			IF CMP(exposureType, "None") == 1
				tv_layerpostbehavior LayerID
				PostBehaviour = result
				IF CMP(PostBehaviour, "Hold") == 0
					Shrink = false // Next frame is empty and layer stops.
					layerRun = 0
				END
			END
		END
		// END RUN ON LAYER
	ELSE
		layerRun = 0
	END
	i = i + 1
END

IF CMP(Shrink, true) == 1
	layerRun = 1
	i = 0
	WHILE layerRun
		tv_LayerGetID i
		LayerID = result
		IF CMP(LayerID, "none") == 0		
			// START RUN ON LAYER
			tv_layerSet LayerID
			Frame = StartFrame
			tv_LayerImage Frame
			tv_exposureinfo Frame
			exposureType = result
			IF CMP(exposureType, "none") == 0
				WHILE CMP(exposureType, "head") == 0
					tv_LayerGetImage
					Frame = result - 1
					tv_LayerImage Frame
					tv_exposureinfo Frame
					exposureType = result
				END
				// On head of exposure
				tv_exposureinfo Frame
				exposureType = result
				tv_exposurenext
				nextInstance = result
				ExposureLength = nextInstance - Frame - 1
				tv_ExposureSet Frame ExposureLength
			END
			// END RUN ON LAYER
		ELSE
			layerRun = 0
		END
		i = i + 1
	END
//ELSE
	//print ("Sorry, can't shrink...")
END

tv_LayerImage StartFrame
tv_layerSet StartLayerID