PostFix = "_v000_X"
Extension = ".tvpp"

tv_getModifiers
PARSE result Shift Ctrl Alt
PreFix = 1
IF CMP(Shift, "1") == 1
	PreFix = 0
END

//Preparing
message = "Saving all Clips as TVP files..."
tv_LockDisplay message
//
tv_GetProjectName
ProjectPath = result

tv_WriteTextFile "Exists" '"'ProjectPath'"'
TestPath = result
// *** Check if file is saved
IF CMP(TestPath,ProjectPath) == 0 || CMP(ProjectPath, "/") == 1 //TestPath is voor windows, slash-check is voor mac? Misschien is TestPath wel overbodig en kan t uberhaupt met alleen de slash-check.
	tv_Warn "Please save project and try again!"
	EXIT
END

// Project Path
ProjectPath = Replace(ProjectPath, "\", "/") //Making the script work on Windows and Unix, Windows has directory seperated by \ and mac and linux with /
StrLen = LEN(ProjectPath)
Last = LastPos(ProjectPath, "/") // Getting the directory where the TVPP file is saved
ProjectDir = LeftString(ProjectPath, Last - 1) // Getting the directory where the TVPP file is saved
ProjectName = RightString(ProjectPath, StrLen - Last) // Getting the name of the tvpp file without the path
FirstUnderscore = FirstPos(ProjectName, "_")
IF CMP(FirstUnderscore, 0) == 0
	ProjectCode = LeftString(ProjectName, FirstUnderscore - 1) // This will include the underscore, f.e. DF01_
ELSE
	ProjectCode = ""
END
// Go up a directory
Last = LastPos(ProjectDir, "/") // Getting the directory where the TVPP file is saved
AboveDir = LeftString(ProjectDir, Last - 1) // Getting the directory where the TVPP file is saved
// Shots directory
ShotsDir = AboveDir"/Shots"

// *********************************************
// DIALOG CODE DUPLICATE MATCH ACTUAL CODE BELOW
// *********************************************
// Dialog warning with path example

//tv_Clipenumid -1 0
//ClipID = result 
//tv_ClipName ClipID
//ClipName = result
//
//UnderscoreFound = Find(ClipName, _, 1)
//UnderscoreFound = Find(ClipName, _, 1)
//IF CMP(UnderscoreFound, 0) == 1 && CMP(PreFix, 1) == 1 // Only add underscores if Prefix == 1 (so no shift is held) AND no underscore is already in ClipName
//	IF CMP(ProjectCode, "")
//		ClipNumberPreFix = addZeroesPostfix("_", i + 1, 3)
//		ClipName = ClipNumberPreFix""ClipName
//	ELSE
//		ClipNumberPreFix = addZeroesPreFix(ProjectCode, i + 1, 3)
//		IF CMP(ClipName, "Untitled") == 0
//			ClipName = ClipNumberPreFix"_"ClipName // 001_Untitled for example
//		ELSE
//			ClipName = ClipNumberPreFix
//		END
//	END
//END

// *********************************************
tv_Request "Export all clips as seperate TVP files in subfolders of '"ShotsDir"'?|OK|Cancel" //1|0
//tv_Request "Export all clips as seperate TVP files in subfolders of '"ShotsDir"'? \n\nFor example:\n'"ShotsDir"/"ClipName"/"ProjectCode""ClipName""PostFix".tvpp'?|OK|Cancel" //1|0
IF result == 0
	EXIT
END

// Create Shots directory
tv_WriteTextFile "MkDir" '"'ShotsDir'"'

tv_Projectcurrentid
SourceProjectID = result

ClipRun = 1
i = 0
WHILE (ClipRun)
	tv_Clipenumid -1 i
	ClipID = result
	IF (CMP(ClipID, "none") != 0)
		ClipRun = 0
	ELSE
		i = i + 1
	END
END

ClipRun = 1
i = 0
WHILE (ClipRun)
	tv_ProjectSelect SourceProjectID  
	tv_Clipenumid -1 i
	ClipID = result

	IF (CMP(ClipID, "none") != 0)
		ClipRun = 0
	ELSE
		tv_Projectduplicate
		tv_Clipenumid -1 i
		ClipID = result 
		tv_ClipHidden ClipID
		ClipIsHidden = result
		IF CMP(ClipIsHidden, 1) == 1
			i = i + 1 // Skip hidden clip and continue to next clip
		ELSE
			tv_ClipName ClipID
			ClipName = result
			IF Find(ClipName, "Shot_", 1)
				ClipName = "Untitled"
			END
			UnderscoreFound = Find(ClipName, _, 1)
			UnderscoreFound = Find(ClipName, _, 1)
			IF CMP(UnderscoreFound, 0) == 1 && CMP(PreFix, 1) == 1 // Only add underscores if Prefix == 1 (so no shift is held) AND no underscore is already in ClipName
				IF CMP(ProjectCode, "")
					ClipNumberPreFix = addZeroesPostfix("_", i + 1, 3)
					DirName = ClipNumberPreFix""ClipName // For example 001_Intro
					FileName = ClipNumberPreFix""ClipName // For example 001_Intro
				ELSE
					FilePreFix = addZeroesPreFix(ProjectCode"_", i + 1, 3)
					DirPreFix = addZeroesPostFix("_", i + 1, 3)
					IF CMP(ClipName, "Untitled") == 0
						DirName = DirPreFix""ClipName // For example 001_Intro
						FileName = FilePreFix"_"ClipName""PostFix""Extension // For example DF01_001_Intro_v000_X.tvpp
					ELSE
						// 
						ClipName = ProjectCode // !!? Debatable!
						//
						DirName = DirPreFix""ClipName // For example 001_DF01 (!!?)
						FileName = FilePreFix""PostFix""Extension // For example DF01_001_v000_X.tvpp
					END
				END
			ELSE
				// For example 003B_BBB
				DirName = ClipName
				FileName = ClipName""PostFix""Extension
			END
			IF (CMP(ClipID, "none") != 0)
				ClipRun = 0
			ELSE
				NewClipRun = 1
				j = 0 // New Clip Numbering
				WHILE (NewClipRun)
					tv_Clipenumid -1 j
					NewClipID = result
					IF (CMP(NewClipID, "none") != 0)
						NewClipRun = 0
					ELSE
						IF (NewClipID != ClipID)
							tv_Clipclose NewClipID
						ELSE
							j = j + 1
						END
					END
				END
				ShotDirPath = ShotsDir"/"DirName
				tv_WriteTextFile "MkDir" '"'ShotDirPath'"'
				tv_SaveProject ShotsDir"/"DirName"/"FileName
				tv_Projectclose
				i = i + 1
			END
		END
	END
END

// *****************************
// *** Include extra functions
// *****************************
#INCLUDE "Include.grg"