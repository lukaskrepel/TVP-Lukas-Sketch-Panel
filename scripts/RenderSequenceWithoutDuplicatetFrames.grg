// ******************************************************************************
// *** RENDER IN/OUT FRAMES AS .PNG SEQUENCE WITH ALPHA CHANNEL IN A SUBFOLDER
// ******************************************************************************
PARAM none
tv_SaveMode "JPG"
ext = ".jpg"
//tv_AlphaSaveMode "NoPreMultiply"
tv_Background "COLOR"
tv_GetProjectName
ProjectPath = result
tv_WriteTextFile "Exists" '"'ProjectPath'"'
TestPath = result
RenderName = "FilenameBro"
// *** Check if file is saved ***
IF CMP(TestPath,ProjectPath) == 0 || CMP(ProjectPath, "/") == 1 //TestPath is voor windows, slash-check is voor mac? Misschien is TestPath wel overbodig en kan t uberhaupt met alleen de slash-check.
	tv_Warn "Please save project and try again!"
	EXIT
END
// *** Duplicate project so we don't mess anything up
tv_Projectduplicate

// **************************************
// *** Hide all storyboard and overlay 
// **************************************
tv_LayerColor hide Display 12 // Hide Storyboard layer before rendering
tv_LayerColor hide Display 11 // Hide Overlay layer before rendering

// *********************************************************************************************
// *** Set layers named "*_Multiply" to multiply. And name all multiplied layers "*_Multiply"
// *********************************************************************************************
SearchWord = Multiply
tv_LayerCurrentID
LayerRun = 1
LayerPos = 0
WHILE LayerRun
	tv_LayerGetID LayerPos	
	lid = result
	IF CMP(lid, "NONE") == 0
		// START RUN ON LAYER
		tv_LayerInfo lid
		PARSE result LayerDisplay LayerPosition LayerOpacity LayerName LayerType LayerStart LayerEnd LayerPreLightTable LayerPostLightTable LayerSelection
                last = LastPos(LayerName, "_")
                StrLen = LEN(LayerName)
                IF CMP(last,0)
                    FirstWord = LayerName
                ELSE
                FirstWord = LeftString(LayerName,Last - 1)
                END
                SecondWord = RightString(LayerName, StrLen - Last)
		IF CMP(SecondWord, SearchWord)
			tv_Layerblendingmode lid Multiply
		END
                tv_Layerblendingmode lid
                IF CMP(result, Multiply)
                        MultiplyLayerName = FirstWord"_Multiply"
                        tv_LayerRename lid MultiplyLayerName
                END
		// END RUN ON LAYER
		LayerPos = LayerPos+1	
	ELSE		
		LayerRun = 0
	END
END

// *** Merge all layers
tv_LayerMergeAll
tv_LayerRecomputeExposure // misschien niet nodig na tv_LayerMergeAll, testen!

// *** Do the stuff
ProjectPath = Replace(ProjectPath,'\','/') //MAKING THE SCRIPT WORK ON WINDOWS AND MAC , Windows has directory seperated by \ and mac and linux with /
StrLen = LEN(ProjectPath)
last = LastPos(ProjectPath, "/") //Getting the directory where the TVPP file is saved
ProjectDir = LeftString(ProjectPath,Last - 1) // Getting the directory where the TVPP file is saved
ProjectName = RightString(ProjectPath, StrLen - Last) //Getting the name of the tvpp file without the path
//Getting the name of the tvpp file without ".tvpp"
last = LastPos(ProjectName, ".")
IF CMP(last, "0") == 1
	ProjectBase = ProjectName
ELSE
	ProjectBase = LeftString(ProjectName,Last - 1)
END
last_v = FindString(ProjectBase, "_v", 1) //File should be name _v001_L or something
IF CMP(last_v, "0") == 1
	RenderName = ProjectBase
ELSE
	RenderName = LeftString(ProjectBase,last_v-1)
END

FOR i = 1 TO 1000
	//todo kijken of seq1 map bestaat, zoja, seq2 maken, etc
	folderName = RenderName"_seq"i
	SaveDir = ProjectDir"/"folderName
	tv_WriteTextFile "MkDir" '"'SaveDir'"' //Now I create again
	IF CMP (result, "Exists") == 0 //Check if folder already exists
		RenderName = folderName
		i = 1001
	END
END
// ***********************************************************************************************************
// Here I get the in point and out point of the clip if the marks is disabled I get the clip in out instead
// ***********************************************************************************************************
tv_MarkIn clip // get clip in
PARSE result frame state
IF CMP(state, "set ") == 1
	InPoint = frame
ELSE
	tv_FirstImage
	InPoint = result
END
//Get Clip Out
tv_MarkOut clip
PARSE result frame state
IF CMP(state, "set ") == 1
	OutPoint = frame
ELSE
	tv_LastImage
	OutPoint = result
END

// *********************************
// *** Getting the filename right
// *********************************
tv_StartFrame
StartFrame = InPoint + result
endFrame = OutPoint+1
frame = addZeros("_",StartFrame,5)
RenderFile = SaveDir"/"RenderName""frame""ext
tv_Request "Render frame "StartFrame" to "endFrame" to '"RenderFile"'?|OK|Cancel" //1|0
IF result == 0
	tv_ProjectClose
	EXIT
END
layer_save(SaveDir, InPoint, OutPoint)
// *** Close duplicated project
tv_ProjectClose

// *******************************
// *** INCLUDE EXTRA FUNCTIONS
// *******************************
#INCLUDE "Include.grg"

// *** STOLEN FUNCTIONS
FUNCTION layer_save(SaveDir, InPoint, OutPoint)
	renderStart = InPoint
	renderEnd = OutPoint
	offsetSTART= renderStart-InPoint+1
	offsetEND = renderEnd-InPoint+1
	// *** Create and write text file with render info
	// * Get info
	renderInfo = SaveDir"/renderInfo.txt"
	in = InPoint + 1
	out = OutPoint + 1
	tv_FrameRate 666 info
	PARSE result projectFramerate previewFramerate
	// * Write info
	tv_WriteTextFile "Create" '"'renderInfo'"' "IN;"in
	tv_WriteTextFile "Append" '"'renderInfo'"' "OUT;"out
	tv_WriteTextFile "Append" '"'renderInfo'"' "FPS;"projectFramerate
	// *** Start saving image files
	CurImage = renderStart
	WHILE CurImage <= renderEnd
		tv_LayerImage CurImage
		offsetImage = CurImage-InPoint+1
		frame = offsetImage + renderStart
		frame = addZeros("_",frame,5)
		imageName = SaveDir"/"RenderName""frame""ext
		tv_saveDisplay imageName
		tv_exposureNext
		CurImage=result
		tv_exposureinfo CurImage
		PARSE result type rest
		IF CMP(type, "Head") == 0
			CurImage=renderEnd+1
		END
	END
END

// **************************************************************************************
// *** Add a prefix and extra zeros to a number, to turn "2" into "_00002" for example
// **************************************************************************************
FUNCTION addZeros(prefix,number,digits)
	LOCAL current i
	current = LEN(number)
	digits = digits - 1
	FOR i = current TO digits
		prefix = prefix"0"
	END
	prefix = prefix""number
	RETURN prefix
END