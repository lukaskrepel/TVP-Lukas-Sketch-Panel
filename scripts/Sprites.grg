// ***************************************************************
// *** 
// *** Mappenstructuur moet zo zijn opgezet:
// *** ProjectName/WorkFolder/001_Intro_v001_L.tvpp
// *** ProjectName/RenderFolder/
// ***
// *** Script maakt dan bijvoorbeeld:
// *** ProjectName/RenderFolder/Mario/Walk_R_00001.png etc.
// *** ProjectName/RenderFolder/Mario/Talk_R_00001.png etc.
// *** ProjectName/RenderFolder/Mario/Idle_R_00001.png etc.
// *** 
// ***************************************************************

RenderFolder = "Sprites" // Set name of the renderfolder in the project folder.

// **********************************
// *** ALLE CLIPS DOORLOPEN START ***
// **********************************
loop=1
i=0
WHILE loop
	tv_ClipEnumID -1 i
 	curCID = result
	IF CMP(curCID, "none") == 1
		loop=0
	ELSE
		tv_ClipHidden curCID
		IF CMP(result, "0") == 1
			tv_ClipSelect curCID
		END
	END
	i=i+1
	IF CMP(curCID, "none") == 0
	
		// *** Hide all storyboard and overlay 
		tv_LayerColor hide Display 12 // Hide Storyboard layer before rendering
		tv_LayerColor hide Display 11 // Hide Overlay layer before rendering
		// *** Fix multiply layers if working on both standard and Pro TVPaint versions
		FixMultiply()

		PARAM none
		tv_SaveMode "PNG"
		ext = ".png"
		tv_AlphaSaveMode "PreMultiply" // Of moet dit NoPreMultiply zijn?
		tv_Background "NONE"
		tv_GetProjectName
		ProjectPath = result
		tv_ClipName
		ClipName = result
		tv_WriteTextFile "Exists" '"'ProjectPath'"'
		TestPath = result

		IF CMP(TestPath,ProjectPath) == 0
			tv_Warn "Please save project and try again!"
		ELSE
			// Save Current Frame So I can set the timelinecursor back at the end of the script
			tv_LayerGetImage
			StartImage = result
			
			//MAKING THE SCRIPT WORK ON WINDOWS AND MAC , Windows has directory seperated by \ and mac and linux with /
			ProjectPath = Replace(ProjectPath,'\','/')
			StrLen = LEN(ProjectPath)
			last = LastPos(ProjectPath, "/") // Getting the directory where the TVPP file is saved
			ProjectDir = LeftString(ProjectPath,Last - 1) // Getting the directory where the TVPP file is saved
			
			//getting the name of the tvpp file without the path
			ProjectName = RightString(ProjectPath, StrLen - Last)
			// getting the name of the tvpp file without ".tvpp"
			StrLen = LEN(ProjectName)
			last = LastPos(ProjectName, ".")
			IF CMP(last, "0") == 1
				ProjectBase = ProjectName
			ELSE
				ProjectBase = LeftString(ProjectName,Last - 2)
			END
				
			//Zoek naam van tvp file om al rendermapnaam te gebruiken, bijv "Mario_v001_L.tvpp" wordt "Mario"
			first = FirstPos(ProjectName, "_")
			ProjectName = LeftString(ProjectName,first-1)
			
			RenderFolder = ProjectName //Alles tot de eerste underscore
			RenderFolder = TVP_Output

			// Create the dir where we will save our sequence   
			SaveDir = ProjectDir"/"RenderFolder"/"ClipName //WAARSCHUWING: shot 001 moet "/001" heten. Ik denk dat de clip gewoon "Walk_R" mag heten
			renderDir = ProjectDir"/"RenderFolder
			clipDir = ProjectDir"/"RenderFolder"/"ClipName
			// First I remove it or delete it so old files will be deleted!!
			//tv_WriteTextFile "Remove" '"'renderDir'"'
			//tv_WriteTextFile "Remove" '"'clipDir'"'
			// now I create again
			tv_WriteTextFile "MkDir" '"'renderDir'"'
			tv_WriteTextFile "MkDir" '"'clipDir'"'
			
			// Here I get the in point and out point of the clip if the marks is disabled I get the clip in out instead
			//get clip in
			tv_MarkIn clip
			PARSE result frame state
			IF CMP(state, "set ") == 1
				InPoint = frame
			ELSE
				tv_FirstImage
				InPoint = result
			END
			//Get Clip Out
			tv_MarkOut clip
			PARSE result frame state
			IF CMP(state, "set ") == 1
				OutPoint = frame
			ELSE
				tv_LastImage
				OutPoint = result
			END
			
			//findProjectInout
			// Because I have to use tv_Projectsavesequence to use the camera out I have to get the clip in and out in the project timeline
			// for this I use tv_Projectcurrentframe
			tv_LayerImage InPoint
			tv_Projectcurrentframe
			ProjIn = result
			tv_Projectcurrentframe ProjIn
			tv_LayerImage OutPoint
			tv_Projectcurrentframe
			ProjOut = result
			tv_Projectcurrentframe ProjOut
			
			// getting the name of the current clip to file prefix
			tv_StartFrame
			StartFrame = InPoint + result
			// setting the name of the first file
			RenderFile = SaveDir"/"ClipName"_0000"StartFrame""ext
			// saving the sequence
			tv_Projectsavesequence '"'RenderFile'"' ProjIn ProjOut "camera"
			// setting the current frame again
			tv_LayerImage StartImage
		END
	END
	tv_Background "COLOR" // WIT KLEURTJE ACHTER POPPEKE WEER AANZETTEN
END // *** EINDE VAN HET CLIPS DOORLOPEN LOOPJE ***

// *****************************
// *** Include extra functions
// *****************************
#INCLUDE "Include.grg"

// *********************************************************************************************
// *** Set layers named "*_Multiply" to multiply. And name all multiplied layers "*_Multiply"
// *********************************************************************************************
FUNCTION FixMultiply()
	SearchWord = Multiply
	tv_LayerCurrentID
	BeginLayer = result
	LayerRun = 1
	LayerPos = 0
	WHILE LayerRun
		tv_LayerGetID LayerPos	
		layerID = result
		IF CMP(layerID, "NONE") == 0
			// START RUN ON LAYER
			tv_LayerInfo layerID
			PARSE result LayerDisplay LayerPosition LayerOpacity LayerName LayerType LayerStart LayerEnd LayerPreLightTable LayerPostLightTable LayerSelection
	                last = LastPos(LayerName, "_")
	                StrLen = LEN(LayerName)
	                IF CMP(last,0)
	                    FirstWord = LayerName
	                ELSE
	                FirstWord = LeftString(LayerName,Last - 1)
	                END
	                SecondWord = RightString(LayerName, StrLen - Last)
			IF CMP(SecondWord, SearchWord)
				tv_Layerblendingmode layerID Multiply
			END
	                tv_Layerblendingmode layerID
	                IF CMP(result, Multiply)
	                        MultiplyLayerName = FirstWord"_Multiply"
	                        tv_LayerRename layerID MultiplyLayerName
	                END
			// END RUN ON LAYER
			LayerPos = LayerPos+1	
		ELSE		
			LayerRun = 0
		END
	END
	tv_LayerSet BeginLayer
END