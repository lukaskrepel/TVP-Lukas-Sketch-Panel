// ******************************************************************************
// *** RENDER IN/OUT FRAMES AS .PNG SEQUENCE WITH ALPHA CHANNEL IN A SUBFOLDER
// ******************************************************************************
 FixMultiply()

PARAM none
tv_saveMode "PNG"
ext = ".png"
tv_AlphaSaveMode "NoPreMultiply"
tv_Background "NONE"
tv_GetProjectName
projectpath = result
tv_WriteTextFile "Exists" '"'projectpath'"'
testPath = result
renderFolder = "TVP_Output"

IF CMP(testPath,projectpath)==0 || CMP(projectpath,"/")==1 //testpath is voor windows, slash-check is voor mac? Misschien is testpath wel overbodig en kan t uberhaupt met alleen de slash-check.
	tv_warn "Please save project and try again!"
ELSE
	tv_layerGetImage //Save Current Frame So I CAN SET IT AT THE END OF SCRIPT
	startImage = result
	projectpath = Replace(projectpath,'\','/') //MAKING THE SCRIPT WORK ON WINDOWS AND MAC , Windows has directory seperated by \ and mac and linux with /
	strlen = LEN(projectpath)
	last = LastPos(projectpath,"/") //Getting the directory where the TVPP file is saved
	projectdir = LeftString(projectpath,last-1) // Getting the directory where the TVPP file is saved
	projectname = RightString(projectpath,strlen-last) //Getting the name of the tvpp file without the path
	//Getting the name of the tvpp file without ".tvpp"
	last = LastPos(projectname,".")
	IF CMP(last,"0")==1
		projectbase = projectname
	ELSE
		projectbase = LeftString(projectname,last-1)
	END
	last_v = FindString(projectbase, "_v", 1) //File should be name _v001_L or something
	IF CMP(last_v,"0")==1
		renderName = projectbase
	ELSE
		renderName = LeftString(projectbase,last_v-1)
	END
	tv_ReqString "Sequence name|"renderName
	IF CMP(result, "Cancel")==1
		EXIT
	ELSE
		renderName = result
	END
	//todo vragen of renderFolder TVP_Output moet zijn of iets anders.
	tv_ReqString "Render folder|TVP_Output"
	IF CMP(result, "Cancel")==1
		EXIT
	END
	renderFolder = result
	saveDir = projectdir"/"renderFolder // Create the dir with the TIFF is saved in	print renderName	
	//WAARSCHUWING: shot 001 moet "/001" heten.
	//tv_WriteTextFile "Remove" '"'saveDir'"' //First I remove it or delete it so old files will be deleted!!
	tv_WriteTextFile "MkDir" '"'saveDir'"' //Now I create again
	IF CMP (result,"Exists")==1 //Check if folder already exists
		//print "Folder already exists: "saveDir
		tv_Request "Folder already exists.|Continue|Cancel" //1|0
		IF result == 0
			EXIT
		END
		tv_Request "Keep existing files?|Keep existing files, overwrite/add new images|Delete existing files" //1|0
		IF result == 0
			tv_Request "Are you sure you want to delete all files in ''"saveDir"''?|Delete files|Cancel" //1|0
			IF result == 1
				tv_WriteTextFile "Remove" '"'saveDir'"' //Delete folder
				tv_WriteTextFile "MkDir" '"'saveDir'"' //Recreate folder
			ELSE
				EXIT
			END
		END
	END
	// ***********************************************************************************************************
	// Here I get the in point and out point of the clip if the marks is disabled I get the clip in out instead
	// ***********************************************************************************************************
	tv_markIn clip // get clip in
	PARSE result frame state
	IF CMP(state,"set ")==1
		inpoint = frame
	ELSE
		tv_firstImage
		inpoint = result
	END
	//Get Clip Out
	tv_markOut clip
	PARSE result frame state
	IF CMP(state,"set ")==1
		outpoint = frame
	ELSE
		tv_LastImage
		outpoint = result
	END
	
	// **************************
	// *** findProject:In/Out
	// **************************
	// Because I have to use tv_projectsavesequence to use the camera out I have to get the clip in and out in the project timeline
	// for this I use tv_projectcurrentframe
	tv_layerImage inpoint
	tv_projectcurrentframe
	projIN = result
	tv_projectcurrentframe projIN
	tv_layerImage outpoint
	tv_projectcurrentframe
	projOUT = result
	tv_projectcurrentframe projOUT	
	// *********************************
	// *** Getting the filename right
	// *********************************
	tv_startFrame
	startFrame = inpoint+result
	//Making sure there's always _##### digits in the framebumber.
	IF startFrame < 10
		startFrame = "_0000"startFrame
	ELSE
		IF startFrame < 100
			startFrame = "_000"startFrame
		ELSE
			IF startFrame < 1000
				startFrame = "_00"startFrame
			ELSE
				IF startFrame < 10000
					startFrame = "_0"startFrame
				ELSE
					startFrame ="_"startFrame
				END
			END
		END
	END
	renderFile = saveDir"/"renderName""startFrame""ext
	tv_projectsavesequence '"'renderFile'"' projIN projOUT "camera"	//saving the image sequence
	tv_layerImage startImage //setting the current frame
END

// *******************************
// *** INCLUDE EXTRA FUNCTIONS
// *******************************
#INCLUDE "Include.grg"