// Thanks to Svengali for fixing the script

tv_getModifiers
PARSE result shift ctrl alt


// Variables
ScriptName = "create_color_fill_layer"
WhiteSpace = "    "

// Load settings
tv_ReadUserString ScriptName "ABColor" "none"
ABColor = result
tv_ReadUserString ScriptName "Overwrite" "none"
Overwrite = result

IF CMP(Shift, "1") == 1 || CMP(ABColor, "none") || CMP(Overwrite, "none")
	// Extenstion
	tv_Request "Use which Pen color?|A|B" //1|0
	ABColor = result
	tv_WriteUserString ScriptName "ABColor" ABColor
	// Overwrite
	tv_Request "Overwrite old color layer?|Overwrite (Needs unique layer names)|No (Safe mode)" //1|0
	Overwrite = result
	tv_WriteUserString ScriptName "Overwrite" Overwrite
	//
	tv_Warn ("New settings saved.\n Click button again to create color fill layer.")
	EXIT
END

//Param None //?

tv_UndoOpenStack

// Remember starting paper
tv_Paperinfo
beginPaper = result
PARSE result z paperHardness z paperInvert z paperSize z paperAngle z paperOffsetX z paperOffsetY z paperFlipX z paperFlipY z paperActive z paperName

tv_LayerGetImage // get original frame number
PARSE result OriginalFrameNo // original frame number
tv_LayerCurrentID // get id for current layer
OriginalLayerID = result // store ID for original layer
tv_LayerInfo OriginalLayerID // get info on original layer
PARSE result d LinePosition d OriginalLayerName d First Last d // get position, first and last frames
IF CMP(Overwrite, 1) == 1
	IF CMP(OriginalLayerName, "Untitled") == 1
		tv_ReqString "Name layer:|"OriginalLayerName
		OriginalLayerName = result
		tv_LayerRename OriginalLayerID OriginalLayerName
	END
END

// Loop trough layers above line layer and set opacity to 0%
Position = LinePosition - 1 // init counter
tv_LayerGetID Position
LayerID = result // init LayerID
While CMP(LayerID, "none") == 0 // loop through all layers to find _color layers
	tv_LayerGetID Position // get LayerID
	LayerID = result
	//tv_Warn ("Position: "Position"   LayerID: "LayerID)
	tv_LayerSet LayerID
	tv_LayerDisplay LayerID Off
	Position = Position - 1 // go up only (counter)
END
tv_LayerSet OriginalLayerID
// All layers above have been set to 0%

LayerNamePreFix = OriginalLayerName
PostFixPos = FindString(OriginalLayerName, "_line", 1)
IF PostFixPos == 0
	LineLayerName = LayerNamePreFix"_line"
	tv_LayerRename OriginalLayerID '"'LineLayerName'"' // Quotes are needed so "Layer 1_color" doesn't cut off after the space.
ELSE
	LayerNamePreFix = LeftString(OriginalLayerName, PostFixPos-1)
END
colorLayerName = LayerNamePreFix"_color"	
TotalFrames = Last - First + 1 // get total selected frames
tv_LayerSelect First TotalFrames // select all frames

// Find and delete old _color layer
IF CMP(Overwrite, 1) == 1
	LayerID = 0	// init LayerID
	Counter = 0 // init counter
	While CMP(LayerID, "none") == 0 // loop through all layers to find _color layers
		tv_LayerGetID Counter // get LayerID
		LayerID = result
		tv_LayerInfo LayerID // get LayerInfo
		PARSE result d d d LayerName d // get Layer name
		IF CMP(LayerName, colorLayerName) // is this a corresponding '_color' layer?
			tv_LayerKill LayerID // yes, so delete it but don't increment counter
		ELSE
			Counter = Counter + 1 // increment counter
		END
	END
END

tv_LayerCreate colorLayerName // create color layer
ColorLayerID = result // store ID for color layer

ColorPosition = LinePosition + 1 // new  position of color 
tv_LayerMove ColorPosition // move color layer to new position
tv_LayerSelect ColorLayerID	// select Color layer
tv_LayerAnim // convert Color layer to anim layer
tv_ExposureSet 0 TotalFrames // extend Color layer frame count 
tv_LayerShift ColorLayerID First // slide Color layer to same position as OriginalLayer
tv_LayerSet OriginalLayerID	// select original layer (with black lines)
X1 = 0 // Designate pixel in upper left corner as seed position
Y1 = 0

tv_GetActiveTool // save tool
Tool = result
PARSE result ToolType d	// get type of tool
tv_Cmd ToolType Mode 0 // set mode to color
tv_GetActiveShape // save shape setting
Shape = result // store activeshape

// Get fill color

IF CMP(ABColor, 1) == 1
	tv_GetAPen
	PARSE result Fr Fg Fb Fa
ELSE
	tv_GetBPen
	PARSE result Fr Fg Fb Fa
END

tv_LayerStencil OriginalLayerID OFF // confirm stencil for line layer is off
tv_LayerStencil FillLayerID OFF	NORMAL // assures that fill layer stencil mask is OFF
SavedFillingShape = result // saves original FillingShape parameters (result always equals previous parameters)
tv_LayerSet ColorLayerID // move to fill layer
tv_AreaInit MODE 1 MAP_GRADIENT 0 OPACITY 100 GAP 0 EXPAND 2 RANGE 10 SRC 1 SMOOTH 1 AALIASING 0 // set fill parameters (user tweakable)
For CurrentFrame = First to Last // loop to fill frames
	tv_LayerImage CurrentFrame // move to current frame on Line layer
	tv_ExposureInfo CurrentFrame // test this frame type" head(instance) or exposure?
	PARSE result FrameType d // store type of frame
	IF CMP(FrameType, "Head") // if frame type is head(instance) then create filled figures for this frame on the Fill Layer 
		tv_LayerSet ColorLayerID // select Color layer
		tv_ExposureBreak CurrentFrame // convert exposure frame to instant if necessary
		tv_Clear // clear Fill Layer
		tv_GetAPen
		APenColor = result // backup A color
		tv_SetAPen 0 0 0 // set A color to black to use as mask
		tv_SetAPen APenColor // restore A color
		tv_Fill X1 Y1 0	 // mask contiguous BACKGROUND around figures
		tv_Pixelmatrix 1 0 0 0 Fr   0 1 0 0 Fg   0 0 1 0 Fb   0 0 0 -1 255	// change black mask into fill color
		tv_LayerSet OriginalLayerID	// select line layer
	END
END

// Loop trough layers above line layer and set opacity back to 100% - TODO instead of 100% remember opacity in first loop and reset it here
LayerID = 0	// init LayerID
Position = LinePosition - 1 // init counter
While CMP(LayerID, "none") == 0 // loop through all layers to find _color layers
	tv_LayerGetID Position // get LayerID
	LayerID = result
	//tv_Warn ("Position: "Position"   LayerID: "LayerID)
	tv_LayerSet LayerID
	tv_LayerDisplay LayerID On
	Position = Position - 1 // go up only (counter)
END
tv_LayerSet OriginalLayerID
// All layers above have been set to 100% - TODO instead of 100% remember opacity in first loop and reset it here

// Match post/pre-behaviour of color layer to line layer
tv_Layerprebehavior OriginalLayerID
tv_Layerprebehavior ColorLayerID result
tv_LayerPostbehavior OriginalLayerID
tv_LayerPostbehavior ColorLayerID result
// Match color group/label of color layer to line layer
tv_LayerColor GET OriginalLayerID
tv_LayerColor SET ColorLayerID result

tv_LayerSet OriginalLayerID	// reselect the drawing layer
tv_LayerImage OriginalFrameNo // return to original frame

tv_UndoCloseStack

tv_SetActiveShape Shape	// restore shape parameters
tv_AreaInit SavedFillingShape // restore FillingShape's original parameters
tv_Cmd Tool	// restore tool and parameters
tv_Paper "active" paperActive "offsetx" paperOffsetX "offsety" paperOffsetY "flipx" paperFlipX "flipy" paperFlipY "hardness" paperHardness "invert" paperInvert "size" paperSize "angle" paperAngle "name" paperName

// Include extra functions
#INCLUDE "include.grg"